#filetag1
#filetag2
title: Start
position: 0,0
---
<<set $shapes_like_you to true>>
Sphere: Hello, I am Blue Sphere.
Cube: Hi there Sphere! I'm Red Cube.
Sphere: And who is this then?

-> I'm Capsule, but my friends call me "Tic Tac". No idea why...
    <<set $name to "Tic Tac">>
-> The name's Triquandle.
    <<set $name to "Triquandle">>
-> Pyramid. Why; who wants to know?
    <<set $name to "Pyramid">>
    <<set $shapes_like_you to false>>

<<if $shapes_like_you>>
Sphere: Nice to meet you {$name}!
Cube: Yeah, likewise!
Cube: Do you wanna play a game?
-> Yeah, sure!
    Sphere: Let's play Two Truths and a Lie then!
    <<jump TwoTruthsGame>>
-> Ugh, no.
<<endif>>
<<jump BadEnding>>
===
title: TwoTruthsGame
position: 200,0
---
<<set $rounds_won to 0>>
Cube: I'll go first! Hmmm...
Cube: I have 6 faces. I have 12 edges. My favourite colour is blue.
Cube: What do you think is the lie?
-> The number of faces.
-> The one about edges.
-> Your favourite colour, obviously.
    <<set $rounds_won to $rounds_won + 1>>
Sphere: I agree.

<<if $rounds_won > 0>>
Cube: You're too smart!
<<else>>
Cube: Wrong! Haha
<<endif>>
Cube: Of course my favourite colour is red!

Sphere: My turn!
Sphere: I am oblate. I am prolate. I am neither.
-> The first one.
-> The second one.
-> The neither.
-> Hang on, that doesn't work.
    That's two lies and one truth!
    <<set $rounds_won to $rounds_won + 1>>
Sphere: Oh wait, I think I've mucked it up...

<<if $rounds_won == 2>>
Cube: You won both rounds, congrats!
<<elseif $rounds_won == 1>>
Cube: Well, at least you got one right.
<<else>>
Sphere: At least we both lost together.
<<endif>>

-> That was great!
    <<jump GoodEnding>>
-> This game is lame.
    <<jump BadEnding>>
===
title: GoodEnding
position: 400,0
---
Cube: I think we're friends now.
Sphere: Agreed.
Cube: What do you think, {$name}?
-> BFFs, for sure!
-> As if I'd be friends with you two!
    <<jump BadEnding>>
===
title: BadEnding
position: 200,120
---
Sphere: No need to be so rude...
Cube: Yeah, maybe you should be called Grumpy {$name}.
Sphere: Ha! Totally.
===
